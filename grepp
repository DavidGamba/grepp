#!/usr/bin/perl -w
use strict;
{
    package Lines;
    sub new
    {
        my $class = shift;
        my $self  = {};
        bless $self, $class;
        $self->initialize(@_);
        return $self;
    }

    sub initialize
    {
        my $self = shift;
        my $hash = shift;

        # prev_size, size of prev array.
        $self->{prev_size} = $hash->{prev_size};
        my $file = $hash->{file};
        open $self->{input}, '<', $file or warn "couldn't open $file $!\n";
        my $in = $self->{input};
    }

    sub next
    {
        my $self = shift;
        my $in = $self->{input};
        my $line = <$in>;
        unshift @{$self->{prev}}, $self->{line};
        if ( $self->{prev_size} ) {
            while($#{$self->{prev}} >= $self->{prev_size}) {
                pop @{$self->{prev}};
            }
        }
        $self->{line} = $line;
        return $self->{line};
    }

    sub current
    {
        my $self = shift;
        return $self->{line} || $self->next;
    }

    sub prev
    {
        my $self   = shift;
        my $number = shift;
        if ( $number ) {
            return ${$self->{prev}}[$number]
        }
        else {
            return ${$self->{prev}}[0]
        }
    }

    sub lines_group
    {
        my $self = shift;
        my $line_group = '';
        my $deep = $self->{prev_size} || 0;
        $deep--;
        while($deep >= 0) {
            $line_group .= $self->prev($deep) if $self->prev($deep);
            $deep--;
        }
        $line_group .= $self->current;
    }

    sub string_group
    {
        my $self = shift;
        my $line_group = '';
        my $deep = $self->{prev_size} || 0;
        $deep--;
        while($deep >= 0) {
            $line_group .= $self->prev($deep) if $self->prev($deep);
            chomp $line_group;
            $deep--;
        }
        $line_group .= $self->current;
        chomp $line_group;
        return $line_group;
    }
}

{
    package Match;
    sub new
    {
        my $class = shift;
        my $self  = {};
        bless $self, $class;
        $self->initialize(@_);
        return $self;
    }

    sub initialize
    {
        my $self = shift;
        my $hash = shift;

        $self->{string} = $hash->{string};
        if ($hash->{case_insensitive}) {
            $self->{regex} = qr/^(.*?)($hash->{regex})(.*)$/is;
        }
        else {
            $self->{regex} = qr/^(.*?)($hash->{regex})(.*)$/s;
        }
    }

    # Return an array of hashes with a single key. match for array entries
    # that match and no_match for the opposite.
    sub match
    {
        my $self    = shift;
        my $string  = shift || $self->{string};
        my @result;
        my $match_found = 0;
        # $1 -> pre match
        # $2 -> regex first match
        # $3 -> after match
        if ($string =~ m/$self->{regex}/) {
            push @result, { no_match => $1 } if $1;
            push @result, { match => $2 }    if $2;
            if ($3) {
                my ($m_f, @after) = $self->match($3);
                push @result, @after;
            }
            $match_found = 1;
        }
        else {
            push @result, { no_match => $string };
        }
        return $match_found, @result;
    }
}

{
    package Color;
    use Term::ANSIColor;
    sub new
    {
        my $class = shift;
        my $self  = {};
        bless $self, $class;
        $self->initialize(@_);
        return $self;
    }

    sub initialize
    {
        my $self = shift;
        my $hash = shift;

        $self->{file_color}  = $hash->{file_color}  || 'magenta';
        $self->{colon_color} = $hash->{colon_color} || 'blue';
        $self->{match_color} = $hash->{match_color} || 'bold red';

        $self->{file_only}   = $hash->{file_only};
        $self->{color}       = $hash->{color} || 'always';
    }

    sub print_file
    {
        my $self = shift;
        my $file = shift;
        $self->set_color($self->{file_color});
        if ($self->{file_only}) {
            print "$file\n";
            return;
        }
        print "$file";
        $self->set_color($self->{colon_color});
        print " : ";
        $self->set_color('reset');
    }

    sub print_match
    {
        my $self = shift;
        $self->set_color($self->{match_color});
        print @_;
        $self->set_color('reset');
    }

    sub set_color
    {
        my $self = shift;
        my $c = shift;
        $c ||= 'reset';
        if ( $self->{color} eq 'always' ) {
            print color "$c";
        }
        else {
            print color 'reset';
        }
    }
}

__PACKAGE__->run() unless caller();

sub run
{
    use Getopt::Long qw(:config passthrough no_ignore_case);
    use Pod::Usage;
    use Data::Dumper;
    use Cwd qw(cwd abs_path);
    use File::Spec::Functions qw( abs2rel );
    use File::Find;

    GetOptions(
        'h|?'       => sub { pod2usage(-verbose => 0) },
        'help'      => sub { pod2usage(-verbose => 1) },
        'man'       => sub { pod2usage(-verbose => 3) },
    );
    my %options = ();
    GetOptions( \%options, 'i', 'fullpath');
    @_ = @ARGV;
    $options{search_pattern} = shift;
    pod2usage("[ERROR] Missing search pattern!\n") unless defined $options{search_pattern};

    my $path = shift;
    $options{cwd} = cwd;
    $path = ($path) ? abs_path($path) : $options{cwd};

    # Set pager and color if not piped output
    END { close STDOUT }
    if (-t STDOUT)
    {
        my $pager = $ENV{PAGER} || "less";
        open(STDOUT, "| $pager");
    }

    find(sub{grep_file(\%options)}, $path);

    close STDOUT;
}

sub grep_file
{
    my $file = $File::Find::name;
    my $options = shift;

    return unless -f $file;

    my $print_file =
        (defined $options->{full_path})
        ? $file
        : abs2rel($file, $options->{cwd});

    my $lines = Lines->new({ file => $file, prev_size => 3 });
    my $match = Match->new(
        {   regex            => $options->{search_pattern},
            case_insensitive => $options->{case}
        }
    );
    my $color = Color->new({ color => $options->{env_color} });
    while ($lines->next)
    {
        $match->{string} = $lines->current;
        my ($match_found, @match_array) = $match->match;
        if ($match_found)
        {
            $color->print_file($print_file);
            foreach my $entry (@match_array)
            {
                $color->print_match($entry->{match}) if $entry->{match};
                print $entry->{no_match} if $entry->{no_match};
            }
        }
    }

}

__END__

=head1 NAME

grepp

=head1 DESCRIPTION

greps B<pattern> recursively from current dir or B<location> in every file
except for files in the git ignore file or given by the cmdline option. It also
can filter by B<file pattern>.

It is inspired in grep and find, but unlike grep it doesn't print a ':'
divider after the file name since that makes it annoying to double click
to select.

As it is expected, the color only shows when the ouput is not being
piped. See B<--color> for more details.

It automatically pipes the output to your environment B<PAGER> or to B<less> if
no environment value is set.

=head1 SYNOPSIS

TODO

=head1 OPTIONS

=over 4

=item B<< <pattern> >>

Pattern to grep for (In regex syntax).

Note: if you need to use B<or> in your regex '(a|b)' do so with
non-returning groups: B<'(?:a|b)'>.

=item B<< <location> >>

Location to search in

=item B< -i >

Ignore case

=item B< -l >

Print file name only

=item B< -I >

Ignore binary files

=item B<< --name | --iname <file pattern> >>

Filter result to match only things that match <file pattern>

=item B<< -v | --ignore <file pattern> >>

Filter result to additionally ignore files that match <file pattern>

=item B<< --spacing >>

Print original spacing.

=item B<< --fullpath >>

Print full paths for matching files

=item B<< --color <color_when> >>

Select color use: never, auto (default), always

=item B<< --pdf >>

Go through pdf files only and convert them to text using pdftotext before
grepping.

=item B<< --lines >>

Number of lines of context to use. It implies B<< --spacing >>.

=back

=head1 BUG REPORTS

David Gamba

=cut
